buildscript {
    ext.kotlin_version = '1.4.10'

    def cmd = 'git rev-list HEAD --count --first-parent'
    ext.version_code = cmd.execute().text.trim().toInteger()
    ext.version_suffix = 'git rev-parse --short HEAD'.execute().text.trim()
    ext.version_name = (version_code / 100).toInteger() + ".${version_code}-${version_suffix}"

    ext.versions = [
            'androidTools': '27.0.1',
    ]

    ext.deps = [
            'lint': [
                    'core'  : "com.android.tools.lint:lint:${versions.androidTools}",
                    'api'   : "com.android.tools.lint:lint-api:${versions.androidTools}",
                    'checks': "com.android.tools.lint:lint-checks:${versions.androidTools}",
                    'tests' : "com.android.tools.lint:lint-tests:${versions.androidTools}",
            ],
    ]

    repositories {
        google()
        jcenter()

    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()

    }
}

apply plugin: 'java-library'
apply plugin: 'kotlin'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compileOnly deps.lint.api
    compileOnly deps.lint.checks
//    lintPublish project(':log_lint')
//    implementation project(':utils_lint')
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8